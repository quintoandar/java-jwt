plugins {
  id 'groovy'
  id 'java'
  id 'maven-publish'
}

group 'br.com.quintoandar'
version '1.5'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
  mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-autoconfigure:2.0.4.RELEASE')
    compile('org.springframework:spring-context:5.0.8.RELEASE')
    compile('org.springframework:spring-beans:5.0.8.RELEASE')
    compile('org.codehaus.groovy:groovy-all:2.4.15')
    compile('commons-io:commons-io:2.6')
    compile('org.bitbucket.b_c:jose4j:0.6.4')
    compile('net.bytebuddy:byte-buddy:1.8.22')

    testCompile('junit:junit:4.12')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.slf4j:slf4j-simple:1.7.21')
}

wrapper {
  gradleVersion = '4.10.2'
}

test {
  afterTest { desc, result ->
    println "Test [${desc.name}] was a ${result.resultType}"
  }
}

javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html4', true)
  }
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  classifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  classifier = 'javadoc'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'br.com.quintoandar'
      artifactId = 'java-jwt'
      version = '1.5'
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = '5A Java JWT'
        description = 'Java JWT parser using QuintoAndar public key'

        scm {
          connection = 'scm:git:git@github.com:quintoandar/java-jwt.git'
          developerConnection = 'scm:git:git@github.com:quintoandar/java-jwt.git'
          url = 'https://github.com/quintoandar/java-jwt'
        }
      }
    }
  }

  repositories {
    maven {
      url = "$projectDir/mvn-repo"
    }
  }
}
